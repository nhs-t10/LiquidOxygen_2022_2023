import java.time.Instant
import java.time.Year
import java.time.temporal.TemporalAdjusters
import java.time.temporal.TemporalQueries

//
// build.gradle in TeamCode
//
// Most of the definitions for building your module reside in a common, shared
// file 'build.common.gradle'. Being factored in this way makes it easier to
// integrate updates to the FTC into your code. If you really need to customize
// the build definitions, you can place those customizations in this file, but
// please think carefully as to whether such customizations are really necessary
// before doing so.


// Custom definitions may go here

// Include common definitions from above.
apply from: '../build.common.gradle'
apply from: '../build.dependencies.gradle'

android {
    namespace = 'org.firstinspires.ftc.teamcode'

    packagingOptions {
        jniLibs.useLegacyPackaging true
    }
}

dependencies {
    // This is REQUIRED! Phones will not be able to connect if this dependency is not included!
    implementation project(':FtcRobotController')

    // KEEP compileOnly HERE! Yes, IntelliJ dislikes it, but IT IS REQUIRED!
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
}

// Set version
String executeCommand(String... args) {
    def os = new ByteArrayOutputStream();

    exec {
        commandLine = args
        standardOutput = os
    }

    return os.toString().trim()
}

String getLatestGitHash() { return executeCommand('git', 'rev-parse', '--short', 'HEAD') }
int getGitCommitCount() { return executeCommand('git', 'log', '--oneline').count("\n") }
String getGitBranch() { return executeCommand('git', 'rev-parse', '--abbrev-ref', 'HEAD') }

version "2.${getGitCommitCount()}+${getLatestGitHash()}@${getGitBranch()}-${System.currentTimeMillis()}"

android.applicationVariants.configureEach { v ->
    tasks.create("javadoc${v.name.capitalize()}", Javadoc) {
        group 'javadoc'
        title "RoboBase"
        destinationDir = file("javadocs-${v.name}")

        source = v.javaCompile.source.findAll {
            it.getAbsolutePath().contains("robobase")
        }

        afterEvaluate {
            classpath = files(v.javaCompile.classpath.files) + files("${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar")
        }
    }
}

file('./src/main/java/liquidoxygen/BuildProperties.java').write("""package liquidoxygen;

// WARNING:
// [!!] This file is automatically generated. Modifications to this file will NOT be SAVED! [!!]

/**
 * Build properties generated at compile-time.
 */
public class BuildProperties {
    /**
     * The version of the software
     */
    public static final String VERSION = "${version}";
}
""")